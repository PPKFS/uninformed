The Brahms Conservatoire is a room.

When play begins:
	let L be a list of numbers;
	say "L is initially [L in brace notation].";
	add 11 to L;
	say "L is now [L in brace notation].";
	add 7 to L;
	add 200 to L;
	add 5 to L;
	add -6 to L;
	add 0 to L;
	say "L is now [L in brace notation].";
	add 5 to L, if absent;
	say "Adding 5 if absent should make no difference: [L in brace notation].";
	reverse L;
	say "L reverses to [L in brace notation].";
	sort L;
	say "L sorts to [L in brace notation].";
	reverse L;
	say "L then reverses to [L in brace notation].";
	say "Entry 2 of L is [entry 2 of L].";
	now entry 3 of L is 104;
	say "After changing entry 3 to 104, L is now [L in brace notation].";
	now entry 3 of L is 208;
	say "After changing entry 3 to 208, L is now [L in brace notation].";
	now entry 1 of L is 1;
	now entry 6 of L is 6;
	say "After changing the two end entries, L is now [L in brace notation].";
	say "These, however, should fail as being out of range.";
	now entry 0 of L is 104;
	now entry 7 of L is 104;
	add 208 to L;
	say "L now contains two instances of 208: [L in brace notation].";
	remove 208 from L;
	say "Both of which go after removing this value: [L in brace notation].";
	let M be a list of numbers;
	add 2 to M; add 3 to M; add 5 to M; add 7 to M; add 11 to M;
	add M to L;
	say "Adding [M in brace notation] to L gives [L in brace notation].";
	remove M from L;
	say "Removing the same set then gives [L in brace notation].";
	extend M to 5 entries;
	say "Extending M to 5 entries should not change it: [M in brace notation].";
	extend M to 7 entries;
	say "Extending M to 7 entries should add two 0s: [M in brace notation].";
	truncate M to 8 entries;
	say "Truncating M to 8 entries should make no difference: [M in brace notation].";
	truncate M to 4 entries;
	say "Truncating M to 4 entries should change it: [M in brace notation].";
	truncate M to the last 2 entries;
	say "Truncating M to the last 2 entries should certainly change it: [M in brace notation].";
	let N be a list of numbers;
	add 7 to N; add 11 to N; add 5 to N; add 16 to N;
	add N to M, if absent;
	say "Adding [N in brace notation] to M, if absent, gives [M in brace notation]."
